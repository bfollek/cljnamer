(ns cljnamer.main
  (:require ["package:flutter/material.dart" :as m]
            ["package:english_words/english_words.dart" :as ew]
            [cljd.flutter :as f]))

(defonce app-state (atom {:current (.random ew/WordPair)}))

(def next-button
  (f/widget
   (m/ElevatedButton
    .onPressed
    #(swap! app-state assoc :current (.random ew/WordPair))
    .child
    (m/Text "Next"))))

(defn big-card
  [pair]
  (f/widget
   :context ctx
   :let [theme (m/Theme.of ctx)]
   (m/Card
    .color (^m/Color .-colorScheme.primary ^m/Theme theme)
    .child
    (m/Padding .padding (m/EdgeInsets.all 20)
               .child   (m/Text (.-asLowerCase ^ew/WordPair pair))))))

(def my-home-page
  (f/widget
   :watch [st app-state]
   :let [pair (:current st)]
   (m/Scaffold
    .body
    (m/Column
     .children [(m/Text "A pseudorandom idea:")
                (big-card pair)
                next-button]))))

(defn main []
  (f/run
   (m/MaterialApp
    .title "ClojureDot Namer App"
    .theme (m/ThemeData .useMaterial3 true
                        .colorScheme (m/ColorScheme.fromSeed .seedColor m/Colors.deepOrange))
    .home my-home-page)))
